/*
 * Copyright (c) 2021 Talent Beyond Boundaries.
 *
 * This program is free software: you can redistribute it and/or modify it under
 * the terms of the GNU Affero General Public License as published by the Free
 * Software Foundation, either version 3 of the License, or any later version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License
 * for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program. If not, see https://www.gnu.org/licenses/.
 */
buildscript {
    ext {
        springBootVersion = '2.7.13'
        isDev = System.properties['env'] == 'dev'
        javaTargetVersion = JavaVersion.VERSION_21
        lombokVersion = "1.18.30"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'idea'
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of('21'))
    }
    sourceCompatibility = 21
    targetCompatibility = 21
}

//Yes - you do have to declare this again (even though it is declared in the
//buildscript above)
//- see https://stackoverflow.com/questions/13923766/gradle-buildscript-dependencies
repositories {
    mavenCentral()
}

dependencies {

    //Instead of defaulting Lombok version, specify recent Lombok version to address this issue:
    // https://stackoverflow.com/questions/65380359/lomboks-access-to-jdk-compilers-internal-packages-incompatible-with-java-16
    //We were getting this error when building server:
    //java: java.lang.IllegalAccessError: class lombok.javac.apt.LombokProcessor
    // (in unnamed module @0x3dd0aa6) cannot access class
    // com.sun.tools.javac.processing.JavacProcessingEnvironment (in module jdk.compiler) because
    // module jdk.compiler does not export com.sun.tools.javac.processing to unnamed module @0x3dd0aa6
    compileOnly("org.projectlombok:lombok:1.18.30")
    annotationProcessor("org.projectlombok:lombok:1.18.30")
    testCompileOnly("org.projectlombok:lombok:1.18.30")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.30")

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    if (!isDev) {
        implementation(project(':ui:admin-portal'))
        implementation(project(':ui:candidate-portal'))
        implementation(project(':ui:public-portal'))
    }
    implementation platform('com.google.cloud:libraries-bom:22.0.0')
    implementation 'com.google.cloud:google-cloud-storage'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20210629-1.32.1'

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.1'
    implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
    implementation group: 'org.apache.poi', name: 'poi-scratchpad', version: '4.1.2'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk16', version: '1.45'
// https://mvnrepository.com/artifact/com.slack.api/slack-api-client
    implementation group: 'com.slack.api', name: 'slack-api-client', version: '1.9.0'
// https://mvnrepository.com/artifact/org.jdom/jdom2
    implementation group: 'org.jdom', name: 'jdom2', version: '2.0.6'
// https://mvnrepository.com/artifact/mysql/mysql-connector-java
    implementation group: 'mysql', name: 'mysql-connector-java', version: '5.1.47'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-webflux')

    //See https://mvnrepository.com/artifact/org.springframework.data/spring-data-elasticsearch
    implementation 'org.springframework.data:spring-data-elasticsearch'

    // https://mvnrepository.com/artifact/org.springframework.security/spring-security-web
    implementation 'org.springframework.security:spring-security-web'

    // https://mvnrepository.com/artifact/org.springframework.security/spring-security-messaging
    implementation 'org.springframework.security:spring-security-messaging'

    //See https://github.com/samdjstevens/java-totp/tree/master/totp-spring-boot-starter
    implementation 'dev.samstevens.totp:totp-spring-boot-starter:1.7.1'

    // https://mvnrepository.com/artifact/com.opencsv/opencsv
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.4'

    // https://github.com/lukas-krecan/ShedLock
    implementation "net.javacrumbs.shedlock:shedlock-spring:5.10.0"
    implementation "net.javacrumbs.shedlock:shedlock-provider-jdbc-template:5.10.0"

    implementation 'com.google.guava:guava:31.1-jre'
    implementation("io.jsonwebtoken:jjwt-api:0.11.2")
    implementation 'com.google.api-client:google-api-client:1.32.1'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.32.1'
    implementation 'com.google.apis:google-api-services-drive:v3-rev20210725-1.32.1'
    implementation("org.flywaydb:flyway-core")
    implementation("org.apache.commons:commons-lang3")
    implementation('com.amazonaws:aws-java-sdk-s3:1.11.656')
    implementation("com.sun.mail:jakarta.mail:2.0.1")
    implementation("jakarta.activation:jakarta.activation-api:latest.release")
    implementation('com.opencsv:opencsv:5.0')
    implementation('commons-beanutils:commons-beanutils:1.9.4')
    implementation('org.thymeleaf.extras:thymeleaf-extras-java8time')
    implementation('org.xhtmlrenderer:flying-saucer-pdf-itext5:9.1.22')
    implementation('net.sf.jtidy:jtidy:r938')
    implementation('org.jsoup:jsoup:1.15.4')

    // Caching
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.2", "io.jsonwebtoken:jjwt-jackson:0.11.2")
    runtimeOnly('org.postgresql:postgresql')
    runtimeOnly('mysql:mysql-connector-java')
    runtimeOnly('com.h2database:h2')
}

jib {
    from {
        image = "eclipse-temurin:21-jre"
    }
    to {
        if (project.hasProperty("test-tc-system")) {
            image = "231168606641.dkr.ecr.us-east-1.amazonaws.com/test-ecs"
        } else if (project.hasProperty("prod-tc-system")) {
            image = "968457613372.dkr.ecr.us-east-1.amazonaws.com/talent-catalog"
        }
        credHelper = 'ecr-login'
    }
}

test {
    useJUnitPlatform()
}

bootJar {
    from(".") {
        include ".ebextensions/**"
    }
}
