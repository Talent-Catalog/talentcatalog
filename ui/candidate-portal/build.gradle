plugins {
  id 'java'
  id "com.github.node-gradle.node" version "2.0.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

node {
  version = '12.4.0'
  download = true
}

//NpmTask runs an npm command with the given arguments.
//https://github.com/srs/gradle-node-plugin/blob/master/src/main/groovy/com/moowork/gradle/node/npm/NpmTask.groovy
//In this case the args specify that the npm run (aka npm run-script) command
//is executed.
//See https://docs.npmjs.com/cli/v6/commands/npm-run-script
//See also https://stackoverflow.com/questions/11580961/sending-command-line-arguments-to-npm-script
//This runs the Angular script "build" ("ng build" - see Package.json) which
//Angular includes in the default created package.json.
//The parameters after the '--' are passed to the Angular build script
//- ie to ng build
//See https://angular.io/cli/build
//So this task effectively runs "ng build --prod --base-href /...-portal/"
task npm_build_ui_bundle(type: NpmTask) {
  args = ['run', 'build', '--', '--prod', '--base-href', '/candidate-portal/']
  dependsOn 'npm_install'
}

jar.dependsOn 'npm_build_ui_bundle'

jar {
  from 'dist' into 'ui-bundle'
}
